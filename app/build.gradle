apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply from: '../config/quality.gradle'

android {
    def keystorePropertiesFile = rootProject.file("keystore.properties");
    def keystoreProperties = new Properties()
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.whooo.babr"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        renderscriptTargetApi 19
        renderscriptSupportModeEnabled true

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    signingConfigs {
        debug {
            storeFile rootProject.file('config/debug.keystore')
            storePassword 'android'
            keyAlias 'android'
            keyPassword 'android'
        }

        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            // Uses new built-in shrinker http://tools.android.com/tech-docs/new-build-system/built-in-shrinker
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    sourceSets {
        release {
            java.srcDirs = ['src/release/java'];
        }
        debug {
            java.srcDirs = ['src/debug/java'];
        }
    }

    dexOptions {
        incremental true
        javaMaxHeapSize "4g"
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    lintOptions {
        quiet true
        abortOnError false
        ignoreWarnings true
        disable 'InvalidPackage'            //Some libraries have issues with this.
        disable 'OldTargetApi'
        //Lint gives this warning but SDK 20 would be Android L Beta.
        disable 'IconDensities'             //For testing purpose. This is safe to remove.
        disable 'IconMissingDensityFolder'  //For testing purpose. This is safe to remove.
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dataBinding {
        enabled = true
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // App's dependencies, including test
    //support libraries
    compile "com.android.support:appcompat-v7:$rootProject.supportLibs"
    compile "com.android.support:design:$rootProject.supportLibs"
    compile "com.android.support:recyclerview-v7:$rootProject.supportLibs"
    compile "com.android.support:cardview-v7:$rootProject.supportLibs"
    //firebase
    compile "com.google.firebase:firebase-core:$rootProject.playServices" //analytics
    compile "com.google.firebase:firebase-database:$rootProject.playServices"
    compile "com.google.firebase:firebase-auth:$rootProject.playServices"
    compile "com.google.firebase:firebase-crash:$rootProject.playServices"

    //google play services
    compile "com.google.android.gms:play-services-auth:$rootProject.playServices"
    //rx
    compile "io.reactivex:rxjava:$rootProject.rxJava"
    compile "io.reactivex:rxandroid:$rootProject.rxAndroid"
    //dagger
    compile "com.google.dagger:dagger:$rootProject.dagger"
    apt "com.google.dagger:dagger-compiler:$rootProject.dagger"
    provided 'javax.annotation:jsr250-api:1.0'
    //retrofit
    compile "com.squareup.retrofit2:retrofit:$rootProject.retrofit"
    compile "com.squareup.retrofit2:converter-jackson:$rootProject.retrofit"
    compile "com.squareup.retrofit2:adapter-rxjava:$rootProject.retrofit"
    compile ("com.squareup.retrofit2:converter-simplexml:$rootProject.retrofit"){
        exclude group: 'xpp3', module: 'xpp3'
        exclude group: 'stax', module: 'stax-api'
        exclude group: 'stax', module: 'stax'
    }
    compile "com.jakewharton.timber:timber:$rootProject.timber"
    compile "com.squareup.picasso:picasso:$rootProject.picasso"
    compile "com.jakewharton.picasso:picasso2-okhttp3-downloader:$rootProject.picassoOkHttpDownloader"
    //facebook
    compile 'com.facebook.android:facebook-android-sdk:4.3.0'

    compile 'com.tbruyelle.rxpermissions:rxpermissions:0.5.2@aar'
    compile 'com.github.kenglxn.QRGen:javase:2.1.0'
    compile 'com.miguelcatalan:materialsearchview:1.4.0'

    // debug tools
    debugCompile "com.squareup.okhttp3:logging-interceptor:$rootProject.okHttp"
    debugCompile "com.facebook.stetho:stetho:$rootProject.stetho"
    debugCompile "com.facebook.stetho:stetho-okhttp3:$rootProject.stetho"
    debugCompile "com.squareup.leakcanary:leakcanary-android:$rootProject.leakCanary"
    testCompile 'junit:junit:4.12'
}

apply plugin: 'com.google.gms.google-services'

