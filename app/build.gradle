apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply from: '../config/quality.gradle'
apply from: '../config/dependencies.gradle'

android {
    compileSdkVersion app.compileSdk
    buildToolsVersion app.buildTools

    compileOptions.encoding = 'UTF-8' // write your encoding here

    defaultConfig {
        applicationId app.applicationId
        vectorDrawables.useSupportLibrary = true
        minSdkVersion app.minSdk
        targetSdkVersion app.targetSdk
        aaptOptions {
            additionalParameters "--no-version-vectors"
        }
        renderscriptTargetApi 19
        renderscriptSupportModeEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFile getDefaultProguardFile('proguard-android.txt')
        }
        debug {
            signingConfig signingConfigs.debug
        }
    }

    sourceSets {
        release {
            java.srcDirs = ['src/release/java'];
        }
        debug {
            java.srcDirs = ['src/debug/java'];
        }
    }

    signingConfigs {
        debug {
            storeFile rootProject.file('config/debug.keystore')
            storePassword 'android'
            keyAlias 'android'
            keyPassword 'android'
        }
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    lintOptions {
        quiet true
        abortOnError false
        ignoreWarnings true
        disable 'InvalidPackage'            //Some libraries have issues with this.
        disable 'OldTargetApi'
        //Lint gives this warning but SDK 20 would be Android L Beta.
        disable 'IconDensities'             //For testing purpose. This is safe to remove.
        disable 'IconMissingDensityFolder'  //For testing purpose. This is safe to remove.
    }

    retrolambda {
        jvmArgs '-noverify'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    // Reactive
    compile libraries.rxJava
    compile libraries.rxAndroid

    // Dagger 2
    compile libraries.dagger
    apt libraries.daggerCompiler
    provided 'javax.annotation:jsr250-api:1.0'

    // Retrofit
    compile libraries.retrofit
    compile libraries.retrofitJacksonConverter
    compile libraries.retrofitRxJavaAdapter
    compile libraries.retrofitGsonConverter
    compile(libraries.retrofitSimpleXmlConverter) {
        exclude group: 'xpp3', module: 'xpp3'
        exclude group: 'stax', module: 'stax-api'
        exclude group: 'stax', module: 'stax'
    }
    //parsing html
    compile 'org.jsoup:jsoup:1.7.3'

    //support libraries
    compile libraries.supportAppCompat
    compile(libraries.supportDesign)
    compile libraries.supportRecyclerView
    compile libraries.supportCardView

    //google services
    compile libraries.googleMaps
    compile libraries.googleLocation

    //utils
    compile libraries.butterKnife
    compile libraries.timber
    compile libraries.picasso
    compile libraries.picassoOkHttpDownloader

    // saving view state
    compile libraries.icepick
    apt libraries.icepickCompiler

    //DBFlow
    compile libraries.dbFlowCore
    compile libraries.dbFlow
    apt libraries.dbFlowProcessor

    compile libraries.parseCore
    compile libraries.parseBolts

    compile(libraries.crashlytics) {
        transitive = true;
    }
    compile libraries.eventbus

    compile 'com.tbruyelle.rxpermissions:rxpermissions:0.5.2@aar'
    compile 'com.pnikosis:materialish-progress:1.7'
    compile 'com.quinny898.library.persistentsearch:library:1.1.0-SNAPSHOT'
    compile 'com.github.kenglxn.QRGen:javase:2.1.0'
    compile 'com.miguelcatalan:materialsearchview:1.4.0'
    compile 'com.github.paolorotolo:appintro:3.3.0'
    compile 'com.nineoldandroids:library:2.4.0'
    compile 'com.bignerdranch.android:recyclerview-multiselect:0.2'
    compile 'com.getbase:floatingactionbutton:1.10.1'

    // debug tools
    debugCompile libraries.okHttpLoggingInterceptor
    debugCompile libraries.stetho
    debugCompile libraries.stethoOkHttp
    debugCompile libraries.leakCanary

    testCompile libraries.junit
    testCompile libraries.robolectric
    testCompile libraries.assertJ
    testCompile libraries.equalsVerifier
    testCompile libraries.mockitoCore
}